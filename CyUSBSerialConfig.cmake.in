if(NOT @PROJECT_NAME@_FIND_COMPONENTS)
    message(CHECK_START "@PROJECT_NAME@: No components specified. Finding all components...")
    set(@PROJECT_NAME@_FIND_COMPONENTS "@components@")
else()
    message(CHECK_START "@PROJECT_NAME@: Finding components ${@PROJECT_NAME@_FIND_COMPONENTS}...")
endif()

set(@PROJECT_NAME@_FOUND TRUE)

foreach(component ${@PROJECT_NAME@_FIND_COMPONENTS})
    message(CHECK_START "looking for ${component}")
    include(${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-${component}Config.cmake)

    if (@PROJECT_NAME@_${component}_FOUND)
        message(CHECK_PASS "found")
        list(APPEND @PROJECT_NAME@_LIBRARIES @PROJECT_NAME@::${component})
    else()
        message(CHECK_FAIL "not found")
        if (@PROJECT_NAME@_FIND_REQUIRED_${component})
            set(@PROJECT_NAME@_FOUND FALSE)
            list(APPEND missing_components ${component})
        endif()
    endif()
endforeach()

if (@PROJECT_NAME@_FOUND)
    message(CHECK_PASS "found")
else()
    message(CHECK_FAIL "not found")
    list(REMOVE_DUPLICATES missing_components)
    set(@PROJECT_NAME@_NOT_FOUND_MESSAGE "Missing components: ${missing_components}")
endif()